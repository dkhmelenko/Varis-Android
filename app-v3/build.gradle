apply plugin: 'com.android.application'
apply plugin: 'jacoco-android'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        applicationId "com.khmelenko.lab.varis"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 12
        versionName "1.00.012"
        vectorDrawables.useSupportLibrary = true
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled true
            // Library specific proguard files
            proguardFile 'proguard-butterknife-7.pro'
            proguardFile 'proguard-eventbus.pro'
            proguardFile 'proguard-gson.pro'
            proguardFile 'proguard-square-retrofit2.pro'
            proguardFile 'proguard-square-okhttp3.pro'
            proguardFile 'proguard-support-v4.pro'
            proguardFile 'proguard-support-v7-appcompat.pro'
            proguardFile 'proguard-dagger.pro'

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

ext {
    libraryVersion = [
            support        : '28.0.0',
            gson           : '2.8.6',
            retrofit2      : '2.4.0',
            circleimageview: '2.1.0',
            dagger         : '2.18',
            glassfish      : '10.0-b28',
            mockito        : '3.5.6',
            junit          : '4.13',
            robolectric    : '4.3.1',
            rxjava         : '2.2.19',
            rxAndroid      : '2.1.1',
            okhttp3        : '3.11.0',
            architecture   : '1.1.1',
            kotlinMockito  : '1.6.0',
            androidX       : '1.1.0',
            lifecycle      : '2.2.0'
    ]
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // support libraries, architecture components
    implementation "androidx.appcompat:appcompat:$libraryVersion.androidX"
    implementation "com.google.android.material:material:$libraryVersion.androidX"
    implementation "androidx.recyclerview:recyclerview:$libraryVersion.androidX"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "androidx.lifecycle:lifecycle-extensions:$libraryVersion.lifecycle"
    kapt "androidx.lifecycle:lifecycle-compiler:$libraryVersion.lifecycle"

    // google
    implementation "com.google.code.gson:gson:$libraryVersion.gson"
    implementation "com.google.dagger:dagger:$libraryVersion.dagger"
    kapt "com.google.dagger:dagger-compiler:$libraryVersion.dagger"
    implementation "com.google.dagger:dagger-android-support:$libraryVersion.dagger"
    kapt "com.google.dagger:dagger-android-processor:$libraryVersion.dagger"

    // retrofit
    implementation "com.squareup.retrofit2:retrofit:$libraryVersion.retrofit2"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$libraryVersion.retrofit2"
    implementation "com.squareup.retrofit2:converter-gson:$libraryVersion.retrofit2"
    implementation "com.squareup.okhttp3:logging-interceptor:$libraryVersion.okhttp3"
    implementation "com.squareup.okhttp3:okhttp:$libraryVersion.okhttp3"

    // rxjava
    implementation "io.reactivex.rxjava2:rxjava:$libraryVersion.rxjava"
    implementation "io.reactivex.rxjava2:rxandroid:$libraryVersion.rxAndroid"

    // others
    implementation "de.hdodenhof:circleimageview:$libraryVersion.circleimageview"
    compileOnly "org.glassfish:javax.annotation:$libraryVersion.glassfish"

    // testing
    androidTestImplementation "org.mockito:mockito-core:$libraryVersion.mockito"
    androidTestImplementation "junit:junit:$libraryVersion.junit"
    testImplementation "org.mockito:mockito-core:$libraryVersion.mockito"
    testImplementation "junit:junit:$libraryVersion.junit"
    testImplementation "org.robolectric:robolectric:$libraryVersion.robolectric"
    testImplementation "com.google.dagger:dagger:$libraryVersion.dagger"
    kaptTest "com.google.dagger:dagger-compiler:$libraryVersion.dagger"
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation "com.nhaarman:mockito-kotlin:$libraryVersion.kotlinMockito"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
